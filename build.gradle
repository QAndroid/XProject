//顶级的构建文件，你可以为所有的sub-projects/modules添加共同的配置选项
//buildscript中，声明的是gradle脚本自身需要使用的资源
buildscript {
    ext.kotlin_version = '1.3.50'

    //插件仓库配置
    repositories {
        google()
        jcenter()
    }

    //依赖插件
    dependencies {
        //Googled的Android Gradle插件
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.tencent.mm:AndResGuard-gradle-plugin:1.2.17'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

//subprojects配置所有子项目共同的配置
//参考：https://proandroiddev.com/reducing-boilerplate-in-gradle-multi-module-projects-2ff2dde5bf95
subprojects {
    //com.android.application和com.android.library必须放在和kotlin-android、kotlin-android-extensions之前
    //如果放在module的build.gradle中，会报错:Extension with name 'android' does not exist. Currently registered extension names: [ext, kotlin]
    if (project.name == 'app') {
        //引入编译构建Gradle插件
        apply plugin: 'com.android.application'
        //引入资源混淆插件
        apply plugin: 'AndResGuard'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: "kotlin-kapt"

    //引入额外Gradle配置文件
    apply from: "${rootProject.rootDir}/config/versions.gradle"

    android {
        //编译的sdk版本
        compileSdkVersion project.ext.compilesdk_version
        //构建工具的版本
        buildToolsVersion project.ext.build_tools_version

        //签名配置，参考：https://developer.android.com/studio/publish/app-signing
        signingConfigs {
            //发布构建签名配置
            release {
                //密钥库文件，一个包含一组私钥的二进制文件，变量保存在gradle.properties中
                storeFile file(STOREFILE)
                //密钥库密码，变量保存在.bash_profile环境变量中，参考：
                //隐藏密码-https://www.jianshu.com/p/714ea34f739a
                //配置签名设置-https://developer.android.com/studio/build/build-variants
                storePassword System.getenv("KSTOREPWD")
                keyAlias = 'xproject'
                keyPassword System.getenv("KKEYPWD")

                //FIXME 为什么System.console()没有提示输入？？
                //if (System.console() != null)
                //  keyPassword System.console().readLine("\nKey password: ")
            }
        }

        //默认配置
        defaultConfig {
            //最低支持版本、支持目标版本
            minSdkVersion project.ext.minsdk_version
            targetSdkVersion project.ext.targetsdk_version

            //测试脚本
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

            //ARouter：annotationProcesser会使用javaCompileOptions配置类获取当前module的名字
            //Kotlin有自己独有的kapt框架做apt相关操作，的报错-There is no route match the path [/xxx/xxx], in group [xxx][ ]"
            //参考：https://juejin.im/post/593e5084128fe1006af179ad
            kapt {
                arguments {
                    arg("AROUTER_MODULE_NAME", project.getName())
                }
            }
        }

        //构建类型
        buildTypes {
            //发布构建类型:代码压缩（混淆），清单文件占位（App名称）
            release {
                //签名配置：使用发布构建签名
                signingConfig signingConfigs.release
            }

            //调试构建类型，仅包含需要的配置，不包含压缩、签名等，加快开发构建速度
            debug {
                //关闭代码压缩——代码压缩会拖慢构建速度，尽可能在调试构建中避免使用
                minifyEnabled false
                shrinkResources false

                debuggable true

                ext.enableCrashlytics = false
            }
        }
    }

    dependencies {
        //主源集依赖项
        implementation fileTree(dir: 'libs', include: ['*.jar'])

        //FIXME 这个具体的作用？？
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        //ARoter:替换成最新版本,需要注意的是api。要与compiler匹配使用，均使用最新版可以保证兼容
        //每个模块需要ARouter apt引用，不然无法在apt中生成索引文件，无法成功跳转
        //每个模块需要ARouter api引用，不然构建报错：ARouter::Compiler The user has configuration the module name, it was [eventbus]
        implementation "com.alibaba:arouter-api:$project.ext.arouter_api_version"
        kapt "com.alibaba:arouter-compiler:$project.ext.arouter_compiler_version"

        //本地测试源集依赖
        testImplementation "junit:junit:$project.ext.junit_version"
        //设备化测试源集依赖
        androidTestImplementation "androidx.test:runner:$project.ext.runner_version"
        androidTestImplementation "androidx.test.espresso:espresso-core:$project.ext.espresso_core_version"
    }

    repositories {
        mavenCentral()
    }
}

//allprojects返回给Project对象以及其所有子项目
allprojects {
    //基础仓库配置
    repositories {
        google()
        jcenter()
    }
}
