apply from: "${rootProject.rootDir}/common_build.gradle"

if (project.ext.isIntroduceDebug) {
    //Application module配置
    project.ext.setAppDefaultConfig project
} else {
    //Lib module配置
    project.ext.setLibDefaultConfig project
}

group = 'workshop1024.com.xproject'
version = '1.0'

android {
    defaultConfig {
        versionCode 1
        versionName "1.0"

        testApplicationId "workshop1024.com.xproject.introduce.test"
    }

    sourceSets {
        main {
            //编译时，构建会选组debug内的资源
            if (project.ext.isIntroduceDebug) {
                //AndroidManifest文件路径
                manifest.srcFile 'src/debug/AndroidManifest.xml'
                //全部资源文件资源
                res.srcDirs = ['src/debug/res', 'src/main/res']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {
                    //排除Java/Debug文件夹下的所有文件
                    exclude 'src/debug/*'
                }
            }
        }
    }

    resourcePrefix "introduce_"
}

dependencies {
    implementation 'workshop1024.com.xproject:base:1.0'
    implementation "androidx.appcompat:appcompat:$project.ext.appcompat_version"

    androidTestImplementation project(':androidTest')
    //fragment测试
    androidTestImplementation "androidx.fragment:fragment-testing:$project.ext.fragment_testing_version"
}